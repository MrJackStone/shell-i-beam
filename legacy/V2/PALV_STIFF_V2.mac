
!!                                                                                                !!
!! 3.6   CRIAR ENRIJECEDORES DE ALMA                                                              !!
!!                                                                                                !!
! Caso a inclusão de enrijecedores de alma esteja habilitada
*if,WebStiffStatus,EQ,1,THEN

   ! Calcular perímetro do retângulo do enrijecedor da alma
   PerYMin=VIGA_y0
   PerZMin=VIGA_z0-(bfMin/2)
   PerYMax=VIGA_y0+VIGA_dm
   PerZMax=VIGA_z0+(bfMin/2)
   PerYMid=VIGA_y0+yOpLine
   PerZMid=VIGA_z0

   ! Determinar se os enrijecedores de alma serão divididos em função dos enrijecedores de abertura
   div8=0
   *if,VIGA_bos,GT,0,THEN
      *if,VIGA_bos,LT,bfMin/2,THEN
         IntZNeg=VIGA_z0-VIGA_bos
         IntZPos=VIGA_z0+VIGA_bos
         div8=1
      *endif
   *endif

   *del,StiffPositions
   *dim,StiffPositions,ARRAY,1
   freePosition=1

   !! 3.6.1   APOIO ESQUERDO                                                                      !!
   !!                                                                                             !!
   *if,WebStiff_LS,EQ,1,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition
      *voper,StiffPositions,TempSP,ADD
      StiffPositions(freePosition)=VIGA_x0
      freePosition=freePosition+1
      *del,TempSP
   *endif

   !! 3.6.2   APOIO DIREITO                                                                       !!
   !!                                                                                             !!
   *if,WebStiff_RS,EQ,1,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition
      *voper,StiffPositions,TempSP,ADD
      StiffPositions(freePosition)=VIGA_x0+VIGA_L
      freePosition=freePosition+1
      *del,TempSP
   *endif

   !! 3.6.3   PONTOS DE INTRODUÇÃO DE CARGA DA FLEXÃO DE 4 PONTOS                                 !!
   !!                                                                                             !!
   *if,WebStiff_4P,EQ,1,AND,LoadType1,EQ,1,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition+1
      *voper,StiffPositions,TempSP,ADD
      StiffPositions(freePosition)=VIGA_x0+(VIGA_L-LoadDistance)/2
      StiffPositions(freePosition+1)=VIGA_x0+(VIGA_L+LoadDistance)/2
      freePosition=freePosition+2
      *del,TempSP
   *endif

   !! 3.6.4   PONTO DE INTRODUÇÃO DE CARGA CONCENTRADA                                            !!
   !!                                                                                             !!
   *if,WebStiff_PL,EQ,1,AND,LoadType3,EQ,1,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition
      *voper,StiffPositions,TempSP,ADD
      StiffPositions(freePosition)=VIGA_x0+PointLoadPos
      freePosition=freePosition+1
      *del,TempSP
   *elseif,WebStiff_PL,EQ,1,AND,LoadType3,GT,1,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition+LoadType3-1
      *voper,StiffPositions,TempSP,ADD
      *do,i,1,LoadType3,1
         StiffPositions(freePosition)=VIGA_x0+PointLoadPos(i)
         freePosition=freePosition+1
      *enddo
      *del,TempSP
   *endif

   !! 3.6.5   POSIÇÕES ARBITRÁRIAS                                                                !!
   !!                                                                                             !!
   *if,WebStiff_X,EQ,1,AND,WebStiff_XQuant,GT,0,THEN
      *voper,TempSP,StiffPositions,ADD
      *del,StiffPositions
      *dim,StiffPositions,ARRAY,freePosition+WebStiff_XQuant-1
      *voper,StiffPositions,TempSP,ADD
      *do,i,1,WebStiff_XQuant,1
         StiffPositions(i+freePosition-1)=WebStiff_XPos(i)
      *enddo
      freePosition=freePosition+WebStiff_XQuant
      *del,TempSP
   *endif

   !! 3.6.6   CRIAR ENRIJECEDORES                                                                 !!
   !!                                                                                             !!
   *if,freePosition,GT,1,THEN
      *del,AllStiffPos
      *dim,AllStiffPos,ARRAY,1
      *get,ParLength,PARM,StiffPositions,DIM,X
      *if,ParLength,GT,0,THEN
         *vfun,StiffPositions,ASORT,StiffPositions
         PrevPos=VIGA_x0-VIGA_L
         *do,i,1,ParLength,1
            XPos=StiffPositions(i)
            *if,XPos,NE,PrevPos,THEN
               *if,XPos,LE,VIGA_L+VIGA_x0,THEN
                  *get,SPCount,PARM,AllStiffPos,DIM,X
                  *del,TmpSP
                  *dim,TmpSP,ARRAY,SPCount
                  *vfun,TmpSP,COPY,AllStiffPos
                  *del,AllStiffPos
                  *dim,AllStiffPos,ARRAY,SPCount+1
                  *do,j,1,SPCount,1
                     AllStiffPos(j)=TmpSP(j)
                  *enddo
                  AllStiffPos(SPCount+1)=XPos
                  *if,div8,EQ,0,THEN
                     SLICEYZ,XPos,PerYMin,PerZMin,PerYMid,PerZMid,1
                     SLICEYZ,XPos,PerYMin,PerZMid,PerYMid,PerZMax,1
                     SLICEYZ,XPos,PerYMid,PerZMin,PerYMax,PerZMid,1
                     SLICEYZ,XPos,PerYMid,PerZMid,PerYMax,PerZMax,1
                  *else
                     SLICEYZ,XPos,PerYMin,PerZMin,PerYMid,IntZNeg,1
                     SLICEYZ,XPos,PerYMin,IntZNeg,PerYMid,PerZMid,1
                     SLICEYZ,XPos,PerYMin,PerZMid,PerYMid,IntZPos,1
                     SLICEYZ,XPos,PerYMin,IntZPos,PerYMid,PerZMax,1
                     SLICEYZ,XPos,PerYMid,PerZMin,PerYMax,IntZNeg,1
                     SLICEYZ,XPos,PerYMid,IntZNeg,PerYMax,PerZMid,1
                     SLICEYZ,XPos,PerYMid,PerZMid,PerYMax,IntZPos,1
                     SLICEYZ,XPos,PerYMid,IntZPos,PerYMax,PerZMax,1
                  *endif
               *endif
               PrevPos=XPos
            *endif
         *enddo
         *get,SPCount,PARM,AllStiffPos,DIM,X
         *del,TmpSP
         *dim,TmpSP,ARRAY,SPCount
         *vfun,TmpSP,COPY,AllStiffPos
         *del,AllStiffPos
         *dim,AllStiffPos,ARRAY,SPCount-1
         *do,j,2,SPCount,1
            AllStiffPos(j-1)=TmpSP(j)
         *enddo
      *endif
   *endif
*endif

! Mesclar entidades do solid model
NUMMRG,KP,1E-8

