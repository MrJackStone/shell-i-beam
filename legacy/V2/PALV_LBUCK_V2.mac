
!! 4.1.5   MODO DE FLAMBAGEM LOCAL                                                                !!
!!                                                                                                !!
*if,GeomImperf6,NE,0,THEN

   *msg,UI,FileName(1),CodeVersion,'GEOMETRIC IMPERFECTION ANALYSIS:','Type 6 (Local Buckling Mode)'
%C (V%f) %/&
%C %/&
%C

   ! Renomear arquivo
   FINISH
   /filname,'GI6FName',1

   ! Carregar modelo
   /prep7
   RESUME,'BaseModel',,,0,1

   ! Inicializar load step
   /solu
   ANTYPE,0
   LSCLEAR,ALL
   NLGEOM,OFF
   PSTRES,ON
   ALLSEL
   SELTOL,1e-8

   ! Apoios
   NSEL,S,LOC,Y,VIGA_y0
   NSEL,A,LOC,Y,VIGA_y0+VIGA_dm
   NSEL,R,LOC,Z,VIGA_z0
   D,ALL,UZ,0,,,,UY
   NSEL,S,LOC,X,VIGA_x0
   D,ALL,UX,0,,,,UY,UZ
   NSEL,S,LOC,X,VIGA_x0+VIGA_L
   NSEL,R,LOC,Z,VIGA_z0
   D,ALL,UY,0,,,,UZ
   NSEL,S,LOC,Z,VIGA_z0-(VIGA_bfs/2)
   NSEL,A,LOC,Z,VIGA_z0+(VIGA_bfs/2)
   NSEL,A,LOC,Z,VIGA_z0-(VIGA_bfi/2)
   NSEL,A,LOC,Z,VIGA_z0+(VIGA_bfi/2)
   D,ALL,UY,0
   ALLSEL
   /solu

   ! Aplicar carga
   NSEL,S,LOC,X,VIGA_x0+VIGA_L
   NSEL,R,LOC,Z,VIGA_z0
   F,ALL,FX,-1

   ! Solucionar load step
   ALLSEL
   /uis,ABORT,OFF
   KEYW,pr_sgui,1
   SOLVE
   KEYW,pr_sgui,0

   *msg,UI,FileName(1),CodeVersion,'GEOMETRIC IMPERFECTION ANALYSIS:','Type 6 (Local Buckling Mode)','Solution is done!'
%C (V%f) %/&
%C %/&
%C %/&
%C

   ! Pós-processo
   /post1
   SET,FIRST

   ! Análise de flambagem
   /solu
   ALLSEL
   ANTYPE,1
   BUCOPT,LANB,ABS(GI6_mode)+5,0,,RANGE
   SOLVE

   ! Pós-processo
   /post1
   SET,,ABS(GI6_mode)
   /show
   PLNSOL,U,SUM

   ! Imagem: deformada da imperfeição geométrica tipo 5
   PALV_TAKEPIC,'_(I6)'
   ALLSEL

   ! Selecionar os nós exclusivamente pertencentes à alma
   NSEL,U,LOC,Y,VIGA_y0
   NSEL,U,LOC,Y,VIGA_y0+VIGA_dm
   NSEL,R,LOC,Z,VIGA_z0
   *get,NodeCount,NODE,0,COUNT
   nthNode=0
   maxFDisp=0
   ! Obter máximo deslocamento (módulo)
   *do,i,1,NodeCount,1
      nthNode=NDNEXT(nthNode)
      *get,fDisp,NODE,nthNode,U,SUM
      fDisp=ABS(fDisp)
      *if,fDisp,GT,maxFDisp,THEN
         maxFDisp=fDisp
      *endif
   *enddo
   ALLSEL

   ! Capturar imagens dos modos de flambagem
   *do,i,1,ABS(GI6_mode)+5
      SET,,i
      /show
      PLNSOL,U,SUM
      ! Imagem: deformada do modo de flambagem
      PALV_TAKEPIC,'_(LMd%i%)'
   *enddo

   ! Calcular o fator de introdução da imperfeição geométrica tipo 3
   UpGeomFactor6=GeomImperf6/maxFDisp

   ALLSEL
   FINISH
*endif




