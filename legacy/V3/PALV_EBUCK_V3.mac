
!! 4.1.4   MODO DE FLAMBAGEM                                                                      !!
!!                                                                                                !!
*if,GeomImperf5,NE,0,OR,BucklingAnalysis,EQ,1,THEN

   *msg,UI,FileName(1),CodeVersion,'GEOMETRIC IMPERFECTION ANALYSIS:','Type 5 (Buckling Mode)'
%C (V%f) %/&
%C %/&
%C

   ! Renomear arquivo
   FINISH
   /filname,'GI5FName',1

   ! Carregar modelo
   /prep7
   RESUME,'BaseModel',,,0,1

   ! Inicializar load step
   /solu
   ANTYPE,0
   LSCLEAR,ALL
   NLGEOM,OFF
   PSTRES,ON
   ALLSEL
   SELTOL,1e-8

   ! Apoios
   PALV_SUP_V3
   /solu

   ! Aplicar carga
   PALV_LOAD_V3

   ! Solucionar load step
   ALLSEL
   SOLVE

   ! Pós-processo
   /post1
   SET,FIRST

   ! Análise de flambagem
   /solu
   ! Determinar o número de modos a extrair
   *if,ABS(GI5_mode),GT,BucklingModes,THEN
      ExtractedModes=ABS(GI5_mode)
   *else
      ExtractedModes=BucklingModes
   *endif
   ALLSEL
   ANTYPE,1
   BUCOPT,LANB,ExtractedModes,0,,RANGE
   /uis,ABORT,OFF
   KEYW,pr_sgui,1
   SOLVE
   KEYW,pr_sgui,0

   *msg,UI,FileName(1),CodeVersion,'GEOMETRIC IMPERFECTION ANALYSIS:','Type 5 (Buckling Mode)','Solution is done!'
%C (V%f) %/&
%C %/&
%C %/&
%C

    ! Caso o tipo de imperfeição geométrica inicial tenha sido solicitado:
   *if,GeomImperf5,NE,0,THEN

      ! Pós-processo
      /post1
      SET,,ABS(GI5_mode)
      /show
      PLNSOL,U,SUM

      ! Imagem: deformada da imperfeição geométrica tipo 5
      PALV_TAKEPIC,'(I5)'
      ALLSEL

      ! Definir quais nós utilizar para calcular o fator de deslocamento
      *if,GI5_nodes,EQ,0,THEN
         ! Selecionar todos os nós da peça alveolar
         NSEL,S,LOC,Z,VIGA_z0
         NSEL,A,LOC,Y,VIGA_y0
         NSEL,A,LOC,Y,VIGA_y0+VIGA_dm
         ESEL,S,TYPE,,TYPE_SHELL181
         NSLE,R
      *elseif,GI5_nodes,EQ,1,THEN
         ! Selecionar os nós exclusivamente pertencentes à alma
         NSEL,U,LOC,Y,VIGA_y0
         NSEL,U,LOC,Y,VIGA_y0+VIGA_dm
         NSEL,R,LOC,Z,VIGA_z0
         ESEL,S,TYPE,,TYPE_SHELL181
         NSLE,R
      *elseif,GI5_nodes,EQ,2,THEN
         ! Selecionar nós da alma e das interfaces alma-mesa
         NSEL,R,LOC,Z,VIGA_z0
         ESEL,S,TYPE,,TYPE_SHELL181
         NSLE,R
      *elseif,GI5_nodes,EQ,3,THEN
         ! Selecionar somente os nós das interfaces alma-mesa
         NSEL,S,LOC,Y,VIGA_y0
         NSEL,A,LOC,Y,VIGA_y0+VIGA_dm
         NSEL,R,LOC,Z,VIGA_z0
         ESEL,S,TYPE,,TYPE_SHELL181
         NSLE,R
      *elseif,GI5_nodes,EQ,4,THEN
         ! Selecionar somente os nós das mesas
         NSEL,S,LOC,Y,VIGA_y0
         NSEL,A,LOC,Y,VIGA_y0+VIGA_dm
         ESEL,S,TYPE,,TYPE_SHELL181
         NSLE,R
      *endif

      *get,NodeCount,NODE,0,COUNT
      nthNode=0
      maxFDisp=0
      ! Obter máximo deslocamento (módulo)
      *do,i,1,NodeCount,1
         nthNode=NDNEXT(nthNode)
         *get,fDisp,NODE,nthNode,U,SUM
         fDisp=ABS(fDisp)
         *if,fDisp,GT,maxFDisp,THEN
            maxFDisp=fDisp
         *endif
      *enddo
      ALLSEL

      ! Calcular o fator de introdução da imperfeição geométrica tipo 3
      UpGeomFactor5=GeomImperf5/maxFDisp
      ALLSEL
   *endif

   ! Caso a análise de flambagem tenha sido solicitada:
   *if,BucklingAnalysis,EQ,1,THEN

      ! Pós-processo
      /post1
      VIEW4W

      ! Inicializar vetor de frequências
      *del,BucklingFreqs
      *dim,BucklingFreqs,ARRAY,BucklingModes

      *do,i,1,BucklingModes,1
         ! Ler enésimo modo de flambagem
         SET,,i
         ! Obter frequência associada ao modo
         *get,nthFreq,MODE,i,FREQ
         BucklingFreqs(i)=nthFreq
         ! Capturar imagens dos modos de vibração
         /show
         PLNSOL,U,SUM
         PALV_BCKPIC_V3,'(BUCKLING%i%)'
      *enddo

   *endif

   FINISH

*endif


